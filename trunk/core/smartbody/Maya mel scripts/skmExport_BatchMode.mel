//  skmExport_BatchMode.mel - part of SmartBody Project
//  Copyright (C) 2005-2008  University of Southern California
//
//  SmartBody is free software: you can redistribute it and/or
//  modify it under the terms of the Lesser GNU General Public License
//  as published by the Free Software Foundation, version 3 of the
//  license.
//
//  SmartBody is distributed in the hope that it will be useful,
//  but WITHOUT ANY WARRANTY; without even the implied warranty of
//  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//  Lesser GNU General Public License for more details.
//
//  You should have received a copy of the Lesser GNU General Public
//  License along with SmartBody.  If not, see:
//      http://www.gnu.org/licenses/lgpl-3.0.txt
//
//  CONTRIBUTORS:
//    Kristin Parker, USC


global proc skmExport_BatchMode( string $outdir )
{
   source export_skm_NoGUI;

   print "Beginning export... \n" ;

   global string $version = "1.0";
   string $AnimDefList[];
   int $ArraySize;
   int $ArrayPos;
   float $animDefsExist = 0;

   if ( !`objExists "SmartbodyExportOptions"` )
   {
      error( "Please run the skm exporter on your file in maya to set up the proper attributes." );
   }

   //Check to see if there are any existing animation definitions
   select -hi SmartbodyExportOptions;
   string $tempList[] = `ls -sl`;


   for ( $obj in $tempList )
   {
      if ( `substring $obj 1 7` == "AnimDef" )
      {
         $animDefsExist = 1;
         $ArraySize = `size( $AnimDefList )`;
         $ArrayPos = $ArraySize;
         $AnimDefList[ $ArrayPos ] = $obj;
         print( "Put " + $obj + " in " + $ArrayPos + "\n" );
      }
   }

   if ( $animDefsExist == 0 )
   {
      error( "There are no AnimDef nodes available. Please run the skm exporter on your file in maya to set up the proper attributes." );
   }

   //run exporter for each animation defintion that exists in the file
   for ( $AnimDef in $AnimDefList )
   {
      int $startFrame = `getAttr( $AnimDef + ".firstFrameNumber" )`;
      int $endFrame = `getAttr( $AnimDef + ".lastFrameNumber" )`;
      string $root = `getAttr( $AnimDef + ".skeletonRoot" )`;

      string $file = "";
      string $fileName = `getAttr( $AnimDef + ".outputFile" )`;

      if ( $outdir == "" )
      {
         //assuming user put in correct path and file
         $file = `getAttr( $AnimDef + ".outputFile" )`;
      }
      else
      {
         if ( endsWith( $outdir, "/" ) )
         {
            $file = $outdir + $fileName;
         }
         else if ( endsWith( $outdir, "skm" ) )
         {
            //user accidently passed an skm name with the directory path
            $file = dirname( $outdir ) + "/" + $fileName;
         }
         else
         {
            $file = $outdir + "/" + $fileName;
         }
      }

      int $fps = `getAttr( $AnimDef + ".framesPerSecond" )`;

      select -r $root;
//      currentUnit -linear "cm";

      skmWriteSkmToFileNoGUI( $file, $AnimDef, $startFrame, $endFrame, $fps, $version );
   }
}
