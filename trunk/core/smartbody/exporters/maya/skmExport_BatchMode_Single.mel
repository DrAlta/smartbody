//  skmExport_BatchMode_Single.mel - part of SmartBody Project
//  Copyright (C) 2005-2008  University of Southern California
//
//  SmartBody is free software: you can redistribute it and/or
//  modify it under the terms of the Lesser GNU General Public License
//  as published by the Free Software Foundation, version 3 of the
//  license.
//
//  SmartBody is distributed in the hope that it will be useful,
//  but WITHOUT ANY WARRANTY; without even the implied warranty of
//  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//  Lesser GNU General Public License for more details.
//
//  You should have received a copy of the Lesser GNU General Public
//  License along with SmartBody.  If not, see:
//      http://www.gnu.org/licenses/lgpl-3.0.txt
//
//  CONTRIBUTORS:
//    Kristin Parker, USC


global proc skmExport_BatchMode_Single( string $outdir, string $animName, string $joint )
{
   source export_skm_NoGUI;

   print "Beginning export... \n";

   global string $version = "1.0";

   if ( !`objExists "SmartbodyExportOptions"` )
   {
      error( "Please run the skm exporter on your file in maya to set up the proper attributes." );
   }

   //Check to see if the animation definition exists
   string $animDef = ( "AnimDef_" + $animName );

   if ( !`objExists $animDef` )
   {
      error( "The passed animation, " + $animName + ", is not present in the file." );
   }


   int $startFrame = `getAttr( $animDef + ".firstFrameNumber" )`;
   int $endFrame = `getAttr( $animDef + ".lastFrameNumber" )`;

   string $root = "";

   if ( $joint == "" )
   {
      $root = `getAttr( $animDef + ".skeletonRoot" )`;
   }
   else
   {
      if ( !`objExists $joint` )
      {
         error( "The passed joint, " + $joint + ", is not present in the file." );
      }

      $root = $joint;
   }

   string $file = "";
   string $stbuffer[];
   int $numTokens = `tokenize $animDef "_" $stbuffer`;

   if ( $outdir == "" )
   {
      //assuming user put in correct path and file
      $file = `getAttr( $animDef + ".outputFile" )`;
   }
   else
   {
      if ( endsWith( $outdir, "/" ) )
      {
         $file = $outdir + $animName + ".skm";
      }
      else if ( endsWith( $outdir, "skm" ) )
      {
         //user passed an skm name with the directory path
         $file = $outdir;
      }
      else
      {
         $file = $outdir + "/" + $animName + ".skm";
      }
   }

   int $fps = `getAttr( $animDef + ".framesPerSecond" )`;

   select -r $root;
//   currentUnit -linear "cm";

   skmWriteSkmToFileNoGUI( $file, $animDef, $startFrame, $endFrame, $fps, $version );
}
