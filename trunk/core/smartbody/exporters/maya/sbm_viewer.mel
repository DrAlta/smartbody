//  sbm_viewer.mel - part of SmartBody Project
//  Copyright (C) 2005-2008  University of Southern California
//
//  SmartBody is free software: you can redistribute it and/or
//  modify it under the terms of the Lesser GNU General Public License
//  as published by the Free Software Foundation, version 3 of the
//  license.
//
//  SmartBody is distributed in the hope that it will be useful,
//  but WITHOUT ANY WARRANTY; without even the implied warranty of
//  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//  Lesser GNU General Public License for more details.
//
//  You should have received a copy of the Lesser GNU General Public
//  License along with SmartBody.  If not, see:
//      http://www.gnu.org/licenses/lgpl-3.0.txt
//
//  CONTRIBUTORS:
//      Yuyu Xu, USC ICT

string $sbmViewerVersion = "0.5";


// get current viewer preference
global proc string getSelectedViewerPreference()
{
	string $selected[] = `textScrollList -q -selectItem scrollListPreference`;
	return ("Viewer_" + $selected[0]);
}

// check whether there is existing preference, if not, create default one
global proc viewerPreferenceCheck()
{
	if ( !`objExists SmartbodyViewerOptions` )
	{
		group -empty -name "SmartbodyViewerOptions";
		setAttr "SmartbodyViewerOptions.visibility" 0;
	}


	// Check to see if there are any existing preference
	select -hi SmartbodyViewerOptions;
	string $prefList[] = `ls -sl`;

	int $numPref = 0;
	for ($pref in $prefList)
	{
		if (startsWith($pref, "Viewer_"))
			$numPref ++;
	}

	if ($numPref == 0)
		$prefList = {"Viewer_Default"};

	// clean preference list first
	textScrollList -e -removeAll scrollListPreference;
	for ($pref in $prefList)
	{
		if (!startsWith($pref, "Viewer_"))
			continue;
		
		// Create New pref Instance if not exist!
		if (!`objExists $pref`)
		{
			group -empty -name $pref -parent SmartbodyViewerOptions;	
		}

		// advanced setting attributes
		if (!`objExists($pref + ".SbmBin")`)
		{
			addAttr -longName "SbmBin" -dataType "string" $pref;
			setAttr -type "string" ($pref + ".SbmBin") "C:/Projects/Pando/scripts/SmartBodyRunTime/bin/sbm-fltk.exe";
		}		
		
		// viewer option attributes
		if (!`objExists($pref + ".SkeletonDir")`)
		{
			addAttr -longName "SkeletonDir" -dataType "string" $pref;
			setAttr -type "string" ( $pref + ".SkeletonDir" ) "";
		}
		if (!`objExists($pref + ".MotionDir")`)
		{
			addAttr -longName "MotionDir" -dataType "string" $pref;
			setAttr -type "string" ( $pref + ".MotionDir" ) "";
		}
		if (!`objExists($pref + ".BindingDir")`)
		{
			addAttr -longName "BindingDir" -dataType "string" $pref;
			setAttr -type "string" ( $pref + ".BindingDir" ) "";
		}		
		if (!`objExists($pref + ".SkeletonName")`)
		{
			addAttr -longName "SkeletonName" -dataType "string" $pref;
			setAttr -type "string" ( $pref + ".SkeletonName" ) "";
		}
		if (!`objExists($pref + ".MotionName")`)
		{
			addAttr -longName "MotionName" -dataType "string" $pref;
			setAttr -type "string" ( $pref + ".MotionName" ) "";
		}		
		if (!`objExists($pref + ".IdleMotionName")`)
		{
			addAttr -longName "IdleMotionName" -dataType "string" $pref;
			setAttr -type "string" ( $pref + ".IdleMotionName" ) "";
		}			
		if (!`objExists($pref + ".MeshName")`)
		{
			addAttr -longName "MeshName" -dataType "string" $pref;
			setAttr -type "string" ( $pref + ".MeshName" ) "";
		}	
		if (!`objExists($pref + ".BindingName")`)
		{
			addAttr -longName "BindingName" -dataType "string" $pref;
			setAttr -type "string" ( $pref + ".BindingName" ) "";
		}
	}
}

// create a new preference
global proc viewerPreferenceCreate()
{
	string $result = `promptDialog -title "Define New Animation" -message "Animation Name?"
					-button "OK" -button "Cancel"
					-defaultButton "OK" -cancelButton "Cancel"
					-dismissString "Cancel"`;
	
	if ($result == "OK")
	{
		$newPrefName = `promptDialog -query -text`;
		// add the preference attribute
		group -empty -name ( "Viewer_" + $newPrefName ) -parent SmartbodyViewerOptions;
		
		// populate the attribute with all data		
		select -hi SmartbodyViewerOptions;
		string $prefList[] = `ls -sl`;		
		viewerPreferenceCheck();
		updatePrefUI();
	}
}

// rename the current preference
global proc viewerPreferenceRename()
{
	// Create "new name" prompt window
	string $PromptResult = `promptDialog -title "Rename Selected Preference" -message "New Preference Name?"
							-button "Rename" -button "Cancel"
							-defaultButton "Rename" -cancelButton "Cancel"
							-dismissString "Cancel"`;
	if ($PromptResult == "Rename")
	{
		string $newPrefName = `promptDialog -q -text`;
		string $selPref[] = `textScrollList -q -selectItem scrollListPreference`; // without prefix "Viewer_"
		string $curPref = getSelectedViewerPreference();	// with prefix "Viewer_"
		if ($curPref != "Viewer_")
		{
			rename ($curPref) ("Viewer_" + $newPrefName);
			textScrollList -e -removeItem $selPref[0] scrollListPreference;
			textScrollList -e -append $newPrefName scrollListPreference;
			textScrollList -e -selectItem $newPrefName scrollListPreference;		
		}
		else
			confirmDialog -title "Hint" -message "Warning: No preference is selected" -button "Ok" -defaultButton "Ok" -dismissString "Ok";
	}
}

global proc viewerPreferenceReset()
{
	string $curPref = getSelectedViewerPreference();
	setAttr -type "string"($curPref + ".SkeletonDir") "";
	setAttr -type "string"($curPref + ".MotionDir") "";
	setAttr -type "string"($curPref + ".BindingDir") "";	
	setAttr -type "string"($curPref + ".SkeletonName") "";
	setAttr -type "string"($curPref + ".MotionName") "";
	setAttr -type "string"($curPref + ".IdleMotionName") "";
	setAttr -type "string"($curPref + ".MeshName") "";
	setAttr -type "string"($curPref + ".BindingName") "";
	
	updatePrefUI();
}

// delete current preference
global proc viewerPreferenceDelete()
{
	string $selPref[] = `textScrollList -q -selectItem scrollListPreference`;
	string $curPref = getSelectedViewerPreference();	
	textScrollList -e -removeItem $selPref[0] scrollListPreference;
	delete $curPref;
	updatePrefUI();
}

// update sk text field
global proc skFileBrowser(string $fileName, string $fileType)
{
	string $curPref = getSelectedViewerPreference();
	setAttr -type "string" ($curPref + ".SkeletonDir") $fileName;
	setAttr -type "string" ($curPref + ".SkeletonName") "";
	updateSkUI();
}

// update skm text field
global proc skmFileBrowser(string $fileName, string $fileType)
{
	string $curPref = getSelectedViewerPreference();
	setAttr -type "string" ($curPref + ".MotionDir") $fileName;
	setAttr -type "string" ($curPref + ".MotionName") "";
	setAttr -type "string" ($curPref + ".IdleMotionName") "";	
	updateSkmUI();
}

// update binding text field
global proc bindingFileBrowser(string $fileName, string $fileType)
{
	string $curPref = getSelectedViewerPreference();
	setAttr -type "string" ($curPref + ".BindingDir") $fileName;
	setAttr -type "string" ($curPref + ".BindingName") "";	
	setAttr -type "string" ($curPref + ".MeshName") "";		
	updateBindingUI();
}

global proc updateSkDirAttr()
{
	string $curPref = getSelectedViewerPreference();
	setAttr -type "string" ($curPref + ".SkeletonDir") `textFieldGrp -q -text skDir`;
	setAttr -type "string" ($curPref + ".SkeletonName") "";	
	updateSkUI();	
}

global proc updateSkmDirAttr()
{
	string $curPref = getSelectedViewerPreference();
	setAttr -type "string" ($curPref + ".MotionDir") `textFieldGrp -q -text skmDir`;
	setAttr -type "string" ($curPref + ".MotionName") "";
	setAttr -type "string" ($curPref + ".IdleMotionName") "";
	updateSkmUI();	
}

global proc updateBindingDirAttr()
{
	string $curPref = getSelectedViewerPreference();
	setAttr -type "string" ($curPref + ".BindingDir") `textFieldGrp -q -text bindingDir`;
	setAttr -type "string" ($curPref + ".BindingName") "";	
	setAttr -type "string" ($curPref + ".MeshName") "";		
	updateBindingUI();
}

// update scroll list attributes
global proc updateSkAttr()
{
	string $curPref = getSelectedViewerPreference();
	string $selected[] = `textScrollList -q -selectItem scrollListSkeleton`;		
	setAttr -type "string"($curPref + ".SkeletonName") $selected[0];	
}

global proc updateSkmAttr()
{
	string $curPref = getSelectedViewerPreference();
	string $selected[] = `textScrollList -q -selectItem scrollListMotion`;
	int $i;
	string $skmAttr;
	for ($i = 0; $i < size($selected); $i = $i + 1)
		$skmAttr = $skmAttr + " " + $selected[$i];	
	setAttr -type "string"($curPref + ".MotionName") $skmAttr;
}

global proc updateIdleSkmAttr()
{
	string $curPref = getSelectedViewerPreference();
	string $selected[] = `textScrollList -q -selectItem scrollListIdleMotion`;		
	setAttr -type "string"($curPref + ".IdleMotionName") $selected[0];
}

global proc updateOpenColladaAttr()
{
	string $curPref = getSelectedViewerPreference();
	string $selected[] = `textScrollList -q -selectItem scrollListBinding`;		
	setAttr -type "string"($curPref + ".BindingName") $selected[0];
}

global proc updateObjAttr()
{
	string $curPref = getSelectedViewerPreference();
	string $selected[] = `textScrollList -q -selectItem scrollListMesh`;	
	int $i;
	string $meshAttr;
	for ($i = 0; $i < size($selected); $i = $i + 1)
		$meshAttr = $meshAttr + " " + $selected[$i];
	setAttr -type "string"($curPref + ".MeshName") $meshAttr;
}

global proc updateSbmBinAttr()
{
	string $curPref = getSelectedViewerPreference();
	setAttr -type "string"($curPref + ".SbmBin") `textFieldGrp -q -text smartbodyBin`;		
}

global proc selectAllMesh()
{
	string $curPref = getSelectedViewerPreference();
	string $allMesh[] = `textScrollList -q -allItems scrollListMesh`;
	int $i;
	for ($i = 0; $i < size($allMesh); $i = $i + 1)
		textScrollList -e -selectItem $allMesh[$i] scrollListMesh;
	updateObjAttr();
}

global proc clearAllMesh()
{
	string $curPref = getSelectedViewerPreference();
	textScrollList -e -deselectAll scrollListMesh;
	updateObjAttr();
}

global proc updateSkUI()
{
	$curPref = getSelectedViewerPreference();
	textScrollList -e -removeAll scrollListSkeleton;
	string $skDir = `getAttr ($curPref + ".SkeletonDir")`;	
	if ($skDir != "")
	{
		string $skeletonName = `getAttr ($curPref + ".SkeletonName")`;		
		if (`filetest -d ($skDir + "/")`)
		{
			string $skFiles[] = `getFileList -folder ($skDir + "/") -filespec "*.sk"`;
			for ($i = 0; $i < size($skFiles); $i = $i + 1)
				textScrollList -e -append $skFiles[$i] scrollListSkeleton;
		}
		else
			confirmDialog -title "Hint" -message "WARNING: Directory Not Valid!" -button "Ok" -defaultButton "Ok" -dismissString "Ok";			
		if ($skeletonName != "")
			textScrollList -e -selectItem $skeletonName scrollListSkeleton;		
	}
	textFieldGrp -e -text $skDir skDir;
}

global proc updateSkmUI()
{
	$curPref = getSelectedViewerPreference();
	textScrollList -e -removeAll scrollListMotion;
	textScrollList -e -removeAll scrollListIdleMotion;
	string $skmDir = `getAttr ($curPref + ".MotionDir")`;
	if ($skmDir != "")
	{
		string $motionName = `getAttr ($curPref + ".MotionName")`;	
		string $idleMotionName = `getAttr ($curPref + ".IdleMotionName")`;
		if (`filetest -d ($skmDir + "/")`)
		{
			string $skmFiles[] = `getFileList -folder ($skmDir + "/") -filespec "*.skm"`;
			for ($i = 0; $i < size($skmFiles); $i = $i + 1)
			{
				textScrollList -e -append $skmFiles[$i] scrollListMotion;
				textScrollList -e -append $skmFiles[$i] scrollListIdleMotion;
			}
		}
		else
			confirmDialog -title "Hint" -message "WARNING: Directory Not Valid!" -button "Ok" -defaultButton "Ok" -dismissString "Ok";
		if ($motionName != "")
		{	
			string $motionList[];
			int $i;
			tokenize $motionName " " $motionList;
			for ($i = 0; $i < size($motionList); $i = $i + 1)
				textScrollList -e -selectItem $motionList[$i] scrollListMotion;		
		}			
		if ($idleMotionName != "")
			textScrollList -e -selectItem $idleMotionName scrollListIdleMotion;				
	}
	textFieldGrp -e -text $skmDir skmDir;	
}

global proc updateBindingUI()
{
	$curPref = getSelectedViewerPreference();
	textScrollList -e -removeAll scrollListBinding;
	textScrollList -e -removeAll scrollListMesh;
	string $bindingDir = `getAttr ($curPref + ".BindingDir")`;	
	if ($bindingDir != "")
	{
		string $bindingName = `getAttr ($curPref + ".BindingName")`;			
		string $meshName = `getAttr ($curPref + ".MeshName")`;	

		if (`filetest -d ($bindingDir + "/")`)
		{
			string $bindingFiles[] = `getFileList -folder ($bindingDir + "/") -filespec "*.xml"`;
			string $objFiles[] = `getFileList -folder ($bindingDir + "/") -filespec "*.obj"`;
			for ($i = 0; $i < size($bindingFiles); $i = $i + 1)
				textScrollList -e -append $bindingFiles[$i] scrollListBinding;
			for ($i = 0; $i < size($objFiles); $i = $i + 1)
				textScrollList -e -append $objFiles[$i] scrollListMesh;				
		}
		else
			confirmDialog -title "Hint" -message "WARNING: Directory Not Valid!" -button "Ok" -defaultButton "Ok" -dismissString "Ok";
		
		if ($bindingName!= "")
			textScrollList -e -selectItem $bindingName scrollListBinding;
		else	// if no default attribute
		{
			string $bindingInfos[] = `textScrollList -q -allItems scrollListBinding`;
			if (size($bindingInfos) == 1)	//if there's only one xml file, just select it
			{
				textScrollList -e -selectItem $bindingInfos[0]	scrollListBinding;
				updateOpenColladaAttr();
			}
		}

		if ($meshName != "")
		{	
			string $meshList[];
			tokenize $meshName " " $meshList;
			for ($i = 0; $i < size($meshList); $i = $i + 1)
				textScrollList -e -selectItem $meshList[$i] scrollListMesh;		
		}
		else
			selectAllMesh();
	}
	textFieldGrp -e -text $bindingDir bindingDir;	
}

// update the UI according to selected viewer preference
global proc	updatePrefUI()
{
	int $i;
	
	// update preference scroll list
	select -hi SmartbodyViewerOptions;
	string $viewerList[] = `ls -sl`;
	string $prefList[] = `textScrollList -q -allItems scrollListPreference`;
	for ( $viewer in $viewerList )
	{
		if (!startsWith( $viewer, "Viewer_" ))
			continue;

		string $viewerSubstring = substituteAllString($viewer, "Viewer_", "");
		int $found = 0;
		for ($pref in $prefList)
		{
			if ($viewerSubstring == $pref)
				$found = 1;
		}
		if (!$found)
			textScrollList -e -append $viewerSubstring scrollListPreference;
	}	
	string $curPref = getSelectedViewerPreference();
	if (($curPref == "Viewer_") && (size($viewerList) > 1))
	{
		textScrollList -e -selectIndexedItem 1 scrollListPreference;	
		$curPref = getSelectedViewerPreference();
	}

	//------ Update other UI	
	if ($curPref == "Viewer_")
	{
		// clear UI
		textScrollList -e -removeAll scrollListSkeleton;
		textScrollList -e -removeAll scrollListMotion;
		textScrollList -e -removeAll scrollListIdleMotion;
		textScrollList -e -removeAll scrollListMesh;
		textScrollList -e -removeAll scrollListBinding;		
		textFieldGrp -e -text "" skDir;	
		textFieldGrp -e -text "" skmDir;	
		textFieldGrp -e -text "" bindingDir;		
		textFieldGrp -e -text "" smartbodyBin;
	}
	else
	{	
		updateSkUI();
		updateSkmUI();
		updateBindingUI();
		// advanced setting
		textFieldGrp -e -text (`getAttr ($curPref + ".SbmBin")`) smartbodyBin;
	}
}

global proc int detectProcess(string $processName)
{
	string $processString = system("tasklist");
	string $processList[];
	tokenize $processString " \n\t" $processList;
	int $found = stringArrayContains($processName, $processList);
	return $found;
}


// this function get called when the "run smartbody" button is pressed
global proc viewerRunButton()
{
	int $noError = 1;
	int $i;
	// getting attr of current preference
	string $curPref = getSelectedViewerPreference();
	string $skeletonDir = `getAttr ($curPref + ".SkeletonDir")`;
	string $motionDir = `getAttr ($curPref + ".MotionDir")`;
	string $smoothBindingDir = `getAttr ($curPref + ".BindingDir")`;
	string $skeleton = `getAttr ($curPref + ".SkeletonName")`;	
	string $motion = `getAttr ($curPref + ".MotionName")`;	
	string $idleMotion = `getAttr ($curPref + ".IdleMotionName")`;
	string $bindingInfo = `getAttr ($curPref + ".BindingName")`;	
	string $mesh = `getAttr ($curPref + ".MeshName")`;	
	string $sbmBin = `getAttr ($curPref + ".SbmBin")`;
	
	
	// getting other setting
	string $binDir = dirname($sbmBin);	
	string $binName = basename($sbmBin, "");		
	string $scriptDir = $binDir + "/../scripts";
	string $seqFile = $scriptDir + "/" + "mayaSbmViewer.seq";
	
	string $character = "defaultCharacter";
	string $motionList[];
	tokenize $motion " " $motionList;
	for ($i = 0; $i < size($motionList); $i = $i + 1)
		$motionList[$i] = basename($motionList[$i], ".skm");	
	string $idleMotionName = basename($idleMotion, ".skm");
	string $skeletonName = basename($skeleton, "");	

	// getting scale factor
	int $scaleChoice = `radioButtonGrp -q -select unitConverterSK`;
	float $scaleSK = 1.0;
	if ($scaleChoice == 1)			$scaleSK = 1.0;
	if ($scaleChoice == 2)			$scaleSK = 30.48;
	if ($scaleChoice == 3)			$scaleSK = 1.0/30.48;
	float $scaleSKM = 1.0;
	$scaleChoice = `radioButtonGrp -q -select unitConverterSKM`;
	if ($scaleChoice == 1)			$scaleSKM = 1.0;
	if ($scaleChoice == 2)			$scaleSKM = 30.48;
	if ($scaleChoice == 3)			$scaleSKM = 1.0/30.48;

	
	//------- Process begins here
	int $foundProcess = detectProcess($binName);
	int $fileExistFlag = `filetest -e $seqFile`;
	if ($fileExistFlag == 1)
		sysFile -delete $seqFile;
		
	if (!`filetest -x $sbmBin`)
		confirmDialog -title "Hint" -message "Sbm Run Time Problem, Please Check Advanced Setting!" -button "Ok" -defaultButton "Ok" -dismissString "Ok";
	else
	{
		if (!$foundProcess)
		{
			// default setting
			system("echo " + "# " + $seqFile + " >> " + $seqFile);
			
			// viewer and camera setting
			system("echo 0.2 viewer open   800 800  50 50" + " >> " + $seqFile);	
			system("echo 0.2 camera eye    0 166 185" + " >> " + $seqFile);	
			system("echo 0.2 camera center 0 92 0" + " >> " + $seqFile);		

			// decide whether to add pre load sequence file
			int $isUsingPreLoadSeq = 0;
			$isUsingPreLoadSeq = `checkBox -q -value checkBoxPreLoad`;
			if ($isUsingPreLoadSeq == 1)
			{
				system("echo 0 path seq " + $scriptDir + " >> " + $seqFile);
				system("echo 1 seq preLoad >> " + $seqFile);
			}
			
			//	load skeleton
			if ($skeleton != "")
			{
				system("echo 0.1 path ME " + "\"" + $skeletonDir + "\""  + " >> " + $seqFile);
				system("echo 3.1 char " + $character + " init " + $skeletonName + " >> " + $seqFile);
				system("echo 3.1 set character " +  $character + " world_offset x 0 y 102 h 0" + " >> " + $seqFile);				
			}
			else
			{
				confirmDialog -title "Hint" -message "Please select a skeleton" -button "Ok" -defaultButton "Ok" -dismissString "Ok";
				$noError = 0;
			}
				
			//	load motion
			if ($motion != "")
			{
				system("echo 0 path ME " + "\"" + $motionDir + "\""  + " >> " + $seqFile);
				system("echo 0 load motions -R " + "\"" + $motionDir + "\"" + " >> " + $seqFile);	
			}
			else
			{
				confirmDialog -title "Hint" -message "Please select a motion" -button "Ok" -defaultButton "Ok" -dismissString "Ok";	
				$noError = 0;
			}	
			
			// check if need to be run under smooth binding mode
			string $meshList[];
			tokenize $mesh " " $meshList;
			int $i;
			if ($bindingInfo != "")
			{
				system("echo 3.2 char " + $character + " smoothbindweight " + "\"" + $smoothBindingDir + "/" + $bindingInfo + "\"" + " >> " + $seqFile);
				if ($mesh != "")
				{
					for ($i = 0; $i < size($meshList); $i = $i + 1)
						system("echo 3.2 char " + $character + " smoothbindmesh " + "\"" + $smoothBindingDir + "/" + $meshList[$i] + "\"" + " >> " + $seqFile);	
					system("echo 3.2 char " + $character + " viewer 4 " + " >> " + $seqFile);
					
					string $runMotion;
					for ($i = 0; $i < size($motionList); $i = $i + 1)
					{
						if ($i == 0)
							$runMotion = "echo 4.5 test bml char " + $character + " ^<animation name=\"" + $motionList[$i] + "\"" + " id=\"anim" + $i + "\""  + "\/^>";
						else
							$runMotion = $runMotion + ("^<animation name=\"" + $motionList[$i] + "\"" + " id=\"anim" + $i + "\"" + " start=\"anim" + ($i - 1) + ":end" + "\"" + "\/^>");
					}
					system($runMotion + " >> " + $seqFile);
					if ($idleMotionName != "")
						system("echo 4 test bml char " + $character + " ^<body posture=\"" + $idleMotionName + "\"\/^>" + " >> " + $seqFile);	
				}
				else
				{
					confirmDialog -title "Hint" -message "Please select a binding xml file" -button "Ok" -defaultButton "Ok" -dismissString "Ok";
					system("echo 3.2 char " + $character + " viewer 0 " + " >> " + $seqFile);
					string $runMotion;
					for ($i = 0; $i < size($motionList); $i = $i + 1)
					{
						if ($i == 0)
							$runMotion = "echo 3.3 test bml char " + $character + " ^<animation name=\"" + $motionList[$i] + "\"" + " id=\"anim" + $i + "\""  + "\/^>";
						else
							$runMotion = $runMotion + ("^<animation name=\"" + $motionList[$i] + "\"" + " id=\"anim" + $i + "\"" + " start=\"anim" + ($i - 1) + ":end" + "\"" + "\/^>");
					}
					system($runMotion + " >> " + $seqFile);
					if ($idleMotionName != "")
						system("echo 3.3 test bml char " + $character + " ^<body posture=\"" + $idleMotionName + "\"\/^>" + " >> " + $seqFile);						
				}
			}
			else
			{
				system("echo 3.3 char " + $character + " viewer 0 " + " >> " + $seqFile);
				string $runMotion;
				for ($i = 0; $i < size($motionList); $i = $i + 1)
				{
					if ($i == 0)
						$runMotion = "echo 4 test bml char " + $character + " ^<animation name=\"" + $motionList[$i] + "\"" + " id=\"anim" + $i + "\""  + "\/^>";
					else
						$runMotion = $runMotion + ("^<animation name=\"" + $motionList[$i] + "\"" + " id=\"anim" + $i + "\"" + " start=\"anim" + ($i - 1) + ":end" + "\"" + "\/^>");
				}
				system($runMotion + " >> " + $seqFile);
				if ($idleMotionName != "")
					system("echo 4 test bml char " + $character + " ^<body posture=\"" + $idleMotionName + "\"\/^>" + " >> " + $seqFile);					
			}		

			// decide whether to add post load sequence file
			int $isUsingPostLoadSeq = 0;
			$isUsingPostLoadSeq = `checkBox -q -value checkBoxPostLoad`;
			if ($isUsingPostLoadSeq == 1)
			{
				system("echo 0 path seq " + $scriptDir + " >> " + $seqFile);
				system("echo 7 seq postLoad >> " + $seqFile);
			}
			
			string $runCommand = "start " + $sbmBin + " -seqpath " + $scriptDir + " -seq " + "mayaSbmViewer.seq" + " -skscale=" + $scaleSK + " -skmscale=" + $scaleSKM;
			int $isUsingFaceBone = 0;
			$isUsingFaceBone = `checkBox -q -value checkBoxPostFaceBone`;
			if ($isUsingFaceBone == 1)
				$runCommand = $runCommand + " -facebone ";
			if ($noError)
				system($runCommand);		
		}
		else
		{
			//	load motion
			if ($motion != "")
			{			
				if ($skeleton != "")
				{
					string $runMotion;
					for ($i = 0; $i < size($motionList); $i = $i + 1)
					{
						if ($i == 0)
							$runMotion = "echo 0 test bml char " + $character + " ^<animation name=\"" + $motionList[$i] + "\"" + " id=\"anim" + $i + "\""  + "\/^>";	
						else
							$runMotion = $runMotion + ("^<animation name=\"" + $motionList[$i] + "\"" + " id=\"anim" + $i + "\"" + " start=\"anim" + ($i - 1) + ":end" + "\"" + "\/^>");
					}
					system($runMotion + " >> " + $seqFile);
					if ($idleMotionName != "")
						system("echo 0 test bml char " + $character + " ^<body posture=\"" + $idleMotionName + "\"\/^>" + " >> " + $seqFile);	
				}
				else
					confirmDialog -title "Hint" -message "Please select a skeleton" -button "Ok" -defaultButton "Ok" -dismissString "Ok";
			}
			string $runCommand = $binDir + "/elsender.exe -m " + "\"sbm seq mayaSbmViewer.seq\"";
			system($runCommand); 
		}
	}
}

global proc helpPreloadButton()
{
	confirmDialog -title "Hint" -message "Please customize your own preLoad.seq file\nIt's under /Pando/scripts/SmartBodyRunTime/scripts\nThere's an example template you can reference" -button "Ok" -defaultButton "Ok" -dismissString "Ok";	
}

global proc helpPostloadButton()
{
	confirmDialog -title "Hint" -message "Please customize your own postLoad.seq file\nIt's under /Pando/scripts/SmartBodyRunTime/scripts\nThere's an example template you can reference" -button "Ok" -defaultButton "Ok" -dismissString "Ok";
}

//--------------------- Main Window ------------------------
global proc sbmViewerGUI()
{
	global string $sbmViewerVersion;
	if ( ( `window -exists viewerMainWin` ) == true )
	{
		deleteUI viewerMainWin;
		windowPref -remove viewerMainWin;
	}	
	
	// create UI Window here
	window -title ("Smartbody Viewer v" + $sbmViewerVersion) -resizeToFitChildren true -sizeable true viewerMainWin;
	columnLayout -adjustableColumn true -columnAttach "both" 5;
	
		frameLayout -label "SmartBody Preferences" -borderStyle etchedIn -collapsable false -collapse 0;
			columnLayout -adjustableColumn true -rowSpacing 2 -columnOffset "both" 10;
				textScrollList  -numberOfRows 3 -width 250 -height 50 -allowMultiSelection false -showIndexedItem 4 -selectCommand updatePrefUI scrollListPreference;
				rowColumnLayout -numberOfRows 1 -rowHeight 1 20 -rowSpacing 1 2 -columnAttach 1 "right" 3;
					button -label "Create Preference" -align "center" -command viewerPreferenceCreate;
					button -label "Rename Preference" -align "center" -command viewerPreferenceRename;
					button -label "Delete Preference" -align "center" -command viewerPreferenceDelete;
					button -label "Reset Preference" -align "center" -command viewerPreferenceReset;
					setParent viewerMainWin;	
	
		frameLayout -label "SK" -borderStyle etchedIn -collapsable true -collapse 0;	
			columnLayout -adjustableColumn true;
				rowColumnLayout -numberOfRows 1 -rowHeight 1 20 -rowSpacing 1 2 -columnAttach 1 "right" 3;
					textFieldGrp -label "SK Directory: " -adjustableColumn 2 -columnAttach 1 "right" 5 -changeCommand updateSkDirAttr skDir;				
					button -label ".." -align "center" -command "fileBrowserDialog -m 4 -fc \"skFileBrowser\" -an \"Select\" -om \"SaveAs\"";	
					setParent ..;		
				text -label "Skeleton List";
				textScrollList -width 250 -height 50 -allowMultiSelection false -showIndexedItem 3 -selectCommand updateSkAttr scrollListSkeleton;
				radioButtonGrp -numberOfRadioButtons 3 -label "Scale(SK)" -labelArray3 "none" "ft to cm" "cm to ft" -select 1 unitConverterSK;
				setParent viewerMainWin;
		
		frameLayout -label "SKM" -borderStyle etchedIn -collapsable true -collapse 0;	
			columnLayout -adjustableColumn true;		
				rowColumnLayout -numberOfRows 1 -rowHeight 1 20 -rowSpacing 1 2 -columnAttach 1 "right" 3;
					textFieldGrp -label "SKM Directory: " -adjustableColumn 2 -columnAttach 1 "right" 5 -changeCommand updateSkmDirAttr skmDir;				
					button -label ".." -align "center" -command "fileBrowserDialog -m 4 -fc \"skmFileBrowser\" -an \"Select\" -om \"SaveAs\"";	
					setParent ..;				
				text -label "Motion List";
				textScrollList -width 250 -height 100 -allowMultiSelection true -showIndexedItem 3 -selectCommand updateSkmAttr -doubleClickCommand viewerRunButton scrollListMotion;				
				text -label "Idle List";
				textScrollList -width 250 -height 100 -allowMultiSelection false -showIndexedItem 3 -selectCommand updateIdleSkmAttr -doubleClickCommand viewerRunButton scrollListIdleMotion;	
				
				radioButtonGrp -numberOfRadioButtons 3 -label "Scale(SKM)" -labelArray3 "none" "ft to cm" "cm to ft" -select 1 unitConverterSKM;
				setParent viewerMainWin;	

		frameLayout -label "Binding" -borderStyle etchedIn -collapsable true -collapse 0;
			columnLayout -adjustableColumn true;
				rowColumnLayout -numberOfRows 1 -rowHeight 1 20 -rowSpacing 1 2 -columnAttach 1 "right" 3;
					textFieldGrp -label "Binding Directory: " -adjustableColumn 2 -columnAttach 1 "right" 5 -changeCommand updateBindingDirAttr bindingDir;				
					button -label ".." -align "center" -command "fileBrowserDialog -m 4 -fc \"bindingFileBrowser\" -an \"Select\" -om \"SaveAs\"";	
					setParent ..;				
				text -label "Binding Info List";
				setParent viewerMainWin;
				
		frameLayout -borderStyle etchedIn -collapsable true -collapse 1 -label "Advanced Binding Options";
			columnLayout -adjustableColumn true;
				textScrollList -width 250 -height 30 -allowMultiSelection false -showIndexedItem 3 -selectCommand updateOpenColladaAttr scrollListBinding;				
				text -label "Mesh List";
				textScrollList -width 250 -height 100 -allowMultiSelection true -showIndexedItem 3 -selectCommand updateObjAttr scrollListMesh;
				rowColumnLayout -numberOfRows 1 -rowHeight 1 20 -rowSpacing 1 2 -columnAttach 1 "right" 3;
					button -label "Select All" -align "center" -command selectAllMesh;
					button -label "Clear All" -align "center" -command clearAllMesh;
					setParent viewerMainWin;				

		frameLayout -borderStyle etchedIn -collapsable true -collapse 1 -label "Other SmartBody Setting";
			columnLayout -adjustableColumn true;
				checkBox -label "Use Facebone" -align "left" -value 1 checkBoxPostFaceBone;
				rowColumnLayout -numberOfRows 1 -rowHeight 1 20 -rowSpacing 1 2 -columnAttach 1 "right" 3;
					checkBox -label "Use Pre -load seq file" -align "left" -value 1 checkBoxPreLoad;
					button -label "help" -align "right" -command helpPreloadButton;
					setParent ..;	
				rowColumnLayout -numberOfRows 1 -rowHeight 1 20 -rowSpacing 1 2 -columnAttach 1 "right" 3;
					checkBox -label "Use Post-load seq file" -align "left" -value 0 checkBoxPostLoad;
					button -label "help" -align "right" -command helpPostloadButton;
					setParent viewerMainWin;	
					
		frameLayout -borderStyle etchedIn -collapsable true -collapse 1 -label "Smooth Binding Exporter";
			columnLayout -adjustableColumn true;
				button -label "Smooth Binding Exporter" -command "source export_smoothbinding.mel; smoothbindingExportGUI";
				setParent viewerMainWin;
				
		frameLayout -borderStyle etchedIn -collapsable true -collapse 1 -label "Advanced Setting";
			columnLayout -adjustableColumn true;
				textFieldGrp -label "SBM Bin: " -adjustableColumn 2 -columnAttach 1 "right" 5 -changeCommand updateSbmBinAttr smartbodyBin;
				setParent viewerMainWin;
	
		frameLayout -borderStyle etchedIn -collapsable false -collapse 0 -labelVisible false;
			columnLayout -adjustableColumn true -rowSpacing 5 -columnOffset "both" 10;
				button -label "Run Smartbody" -command viewerRunButton;
				setParent viewerMainWin;								
	
	// Intialization to Viewer UI
	viewerPreferenceCheck();
	updatePrefUI();
	
	// Show the Main Window
	showWindow viewerMainWin;
}

/*--------------------------------------------
VERSION HISTORY:
  0.1 - first version
  0.2 - supporting viewer preference, smooth binding
  0.3 - add quick test mode. If sk and skm text field is filled up, this mode is activated
  0.4 - re-design the UI to make the tool more flexible
  0.5 - add the smooth binding exporter
  
LIMIT:
  - after sbm-fltk window starts, user can only change the motion and idle motion inside the scroll list
  - when open a new scene inside one scene, viewer window has to be re-lauched because SmartbodyViewerOption Node is gone in the new scene
  - open collada binding the mesh to the binding pose, SmartBody starts from T pose, which means exporting smoothbinding has to occur at T pose, otherwise the animation would be messy
  - now the morph targets mesh exported works under SkinRig Scene, not working under AnimReady Scene
  - obj Exporter (coming with Maya) is exporting only centimeter, so to solve the inconsistency, I enforce the unit to be centimeter when exporting open collada and obj and change the unit back afterwards.
*/