//  skExport_BatchMode.mel - part of SmartBody Project
//  Copyright (C) 2005-2008  University of Southern California
//
//  SmartBody is free software: you can redistribute it and/or
//  modify it under the terms of the Lesser GNU General Public License
//  as published by the Free Software Foundation, version 3 of the
//  license.
//
//  SmartBody is distributed in the hope that it will be useful,
//  but WITHOUT ANY WARRANTY; without even the implied warranty of
//  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//  Lesser GNU General Public License for more details.
//
//  You should have received a copy of the Lesser GNU General Public
//  License along with SmartBody.  If not, see:
//      http://www.gnu.org/licenses/lgpl-3.0.txt
//
//  CONTRIBUTORS:
//    Kristin Parker, USC



////////////////////////////////////////////////////////////////
//  Calls the skExport.mel script once it prepares the inputs
//  passed to it from the batch file.


// if parameter is an empty string, then value from MAYA file is pulled
global proc skExport_BatchMode( string $r, string $dir, string $n, string $sA, string $uL )
{
   source export_sk_NoGUI;
   global string $version = "0.3";

   if ( !`objExists "SkelDef"` )
   {
      //error will print this message and exit the script
      error( "File is missing skeleton definition attributes. Please run the export_sk.mel script on this file in Maya.\n" );
   }

   string $root = "";
   if ( $r == "" )
   {
      $root = `getAttr SkelDef.RootJoint`;
   }
   else
   {
      $root = $r;
   }

   select -r $root;

   string $file = "";
   if ( $dir == "" )
   {
      $file = `getAttr SkelDef.OutputDirectory`;
   }
   else
   {
      $file = $dir;
   }

   string $name = "";
   if ( $n == "" )
   {
      $name = `getAttr SkelDef.SkeletonName`;
      if ( endsWith( $file, ".sk" ) )
      {
         //path already has filename in it, do nothing for now
      }
      else
      {
         $file = $file + "/" + $name + ".sk";
      }
   }
   else
   {
      $name = $n;
      if ( !endsWith( $file, ".sk" ) )
      {
         //we've been given a different file name, so let's put it in
         $file = $file + $name + ".sk";
      }
      else
      {
         $temp = `dirname( $file )` + "/" + $name + ".sk";
         $file = $temp;
      }
   }

   int $showAll = 0;
   if ( $sA == "showAll" )
   {
      $showAll = 1;
   }
   else if ( $sA == "onlyLimited" )
   {
      $showAll = 0;
   }
   else
   {
      $showAll = `getAttr SkelDef.ShowAllChannels`;
   }

   int $useLimits = 0;
   if ( $uL == "useLimits" )
   {
      $useLimits = 1;
   }
   else if ( $uL == "ignoreLimits" )
   {
      $useLimits = 0;
   }
   else
   {
      $useLimits = `getAttr SkelDef.UseLimits`;
   }

   currentUnit -linear "cm";

   export_sk_NoGUI( $file, $name, $version, $showAll, $useLimits );
}
